: FLOATS CELLS ;
: FLOAT+ CELL+ ;

: DFLOATS 3 LSHIFT ;
: DFLOAT+ 8 + ;
: SFLOATS 2 LSHIFT ;
: SFLOAT+ 4 + ;

: FALIGNED DUP -1 FLOATS AND IF -1 FLOATS AND 1 FLOATS + THEN ;
: SFALIGNED DUP -1 SFLOATS AND IF -1 SFLOATS AND 1 SFLOATS + THEN ;
: DFALIGNED DUP -1 DFLOATS AND IF -1 DFLOATS AND 1 DFLOATS + THEN ;

: FALIGN HERE FALIGNED ORG ;
: SFALIGN HERE SFALIGNED ORG ;
: DFALIGN HERE DFALIGNED ORG ;

: FFIELD: FALIGNED 1 FLOATS +FIELD ;
: SFFIELD: SFALIGNED 1 SFLOATS +FIELD ;
: DFFIELD: DFALIGNED 1 DFLOATS +FIELD ;

: FVARIABLE CREATE 1 FLOATS ALLOT ;
: FCONSTANT
    CREATE
    1 FLOATS ALLOT HERE 1 FLOATS - F!
    DOES> F@
;
1E 0E F/ FABS FCONSTANT +INF
+INF FNEGATE  FCONSTANT -INF

: F2DUP FOVER FOVER ;

: F~
    FDUP ISNAN? IF FDROP FDROP FDROP 0 EXIT THEN
    FDUP 0e0 F< IF
        \ flag negative
        FABS \ r1 r2 |r3|
        FROT FROT \ |r3| r1 r2
        F2DUP F- FABS \ |r3| r1 r2 r1-r2
        FROT FABS FROT FABS \ |r3| r1-r2 |r1| |r2|
        F+ \ |r3| r1-r2 |r1|+|r2|
        FROT F* \ r1-r2 |r1|+|r2|*|r3|
        F<
    ELSE
        FDUP F0= IF
            \ flag zero
            FDROP F>R F>R =
        ELSE
            \ flag positive
            FROT FROT F- FABS FSWAP F<
        THEN
    THEN
;

DECIMAL

: FBUF STATE SYS:PO ;
: FBUFSIZE 256 ;
: CLEAR-FBUF FBUF FBUFSIZE 32 FILL ;

: .MANT  ( u -- )
    FBUF OVER TYPE [CHAR] . EMIT
    FBUF PRECISION ROT /STRING TYPE
;

: F.  ( r -- )
    CLEAR-FBUF
    FBUF PRECISION REPRESENT IF
        IF [CHAR] - EMIT THEN
        DUP >R 0 PRECISION 1+ WITHIN IF
            R> .MANT
        ELSE
            1 .MANT [CHAR] E EMIT R> 1- .
        THEN
    ELSE
        2DROP FBUF FBUFSIZE -TRAILING TYPE
    THEN
    SPACE
;

: FS.  ( r -- )
    CLEAR-FBUF
    FBUF PRECISION REPRESENT IF
        IF [CHAR] - EMIT THEN
        1 .MANT [CHAR] E EMIT 1- .
    ELSE
        2DROP FBUF FBUFSIZE -TRAILING TYPE
    THEN
    SPACE
;

: FE.  ( r -- )
    CLEAR-FBUF
    FBUF PRECISION REPRESENT IF
        IF [CHAR] - EMIT THEN
        1- S>D 3 FM/MOD 3 * >R
        1+ .MANT [CHAR] E EMIT R> .
    ELSE
        2DROP FBUF FBUFSIZE -TRAILING TYPE
    THEN
    SPACE
;

